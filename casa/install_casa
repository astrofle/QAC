#! /bin/csh -f
#
#   Example casa install
#
#   By default this will install the latest released casa under the QAC/casa directory.
#   Adding CASAROOT/bin and CASAROOT/lib/casa/bin is the responsibility of the user.
#
#   Usage:
#
#     install_casa [url]
#
#   By default it will install the latest release, but optionally a URL with the tar.gz file
#   can be given. Examples below.
#
#   7-mar-2018  Peter Teuben      initial version written for SD2018
#
#  TODO's:
#  - test to check if everything was installed properly and working ok
#  - use modules to load different versions (or astromake)
#

#   make it (sort of) work for Linux and Darwin (MacOSX)
set uname = `uname -s`

if ($uname == Darwin) set uname = osx
if ($uname == Linux)  set uname = linux

set o1 =  https://casa.nrao.edu/download/distro/osx/release/10.12/casa-release-5.1.2-4-10.12.dmg.md5
set o2 =  https://casa.nrao.edu/download/distro/osx/prerelease/10.13/casa-prerelease-5.3.0-89-10.13.dmg

set l1 = https://casa.nrao.edu/download/distro/linux/release/el7/casa-release-5.1.2-4.el7.tar.gz
set l2 = https://casa.nrao.edu/download/distro/linux/release/el7/casa-release-5.1.1-5.el7.tar.gz
set l3 = https://casa.nrao.edu/download/distro/linux/prerelease/el7/casa-prerelease-5.3.0-89.el7.tar.gz

if ($uname == osx) then
   echo Sorry, I cannot help you yet dave
   echo get the dmg, e.g.
   echo $o1
   echo $o2
   echo And drag the app into /Applications
   exit 0
else
   echo Running on linux, good. Some common versions you might use
   echo $l1
   echo $l2
   echo $l3
endif

set pre = https://casa.nrao.edu/download/distro/$uname/prerelease/el7/
set rel = https://casa.nrao.edu/download/distro/$uname/release/el7/



if ($#argv > 0) then
   set url = $1
else
   set url = $l1
endif

echo Using $url

#  no changes below here

set targz = $url:t
set dir   = $targz:r:r


if (-d $dir) then
    echo $dir already exists, not installing again
else
    curl $url | tar zxf -
endif


set root = `pwd`
set bin1=$root/$dir/bin
set bin2=$root/$dir/lib/casa/bin

echo "set path = ($bin1 $bin2 "'$path); rehash'  >> casa_start.csh
echo "export PATH=${bin1}:${bin2}:"'$PATH'       >> casa_start.sh

